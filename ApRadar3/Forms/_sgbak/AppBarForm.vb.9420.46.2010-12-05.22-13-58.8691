Imports FFXIMemory
Imports DataLibrary
Imports DataLibrary.DataAccess
Imports DataLibrary.ApRadarDataSet
Imports RadarControls
Imports ApRadar3.AppBarManager
Imports FFXIMemory.MobData

Public Class AppBarForm
#Region " MEMBER VARIABLES "
    Private WithEvents _abm As AppBarManager
    Private _bf As BrowserForm
    Private WithEvents _orf As OverlayRadarForm
    Private WithEvents _mrf As MappedRadarForm
    Private WithEvents _mdf As MobDataForm
    Private WithEvents _pcf As PCInfoForm
    Private _dbf As DataBrowserForm
    Private _debugForm As DebugForm

    Private _mob As DataLibrary.ApRadarDataSet.MobsRow
    Private _targetInfoData As String
    Private _isTargetInfoDropped As Boolean
    Private _isPCInfoDropped As Boolean

    Private _screen As Screen
    Private _sof As FormAnimator
    Private WithEvents _timer As Timer
    Private _isAutoHide As Boolean
    Private _isHidden As Boolean
    Private WithEvents _targetTimer As Timer

    Private _ffxi As FFXI

    Private _mem As Memory
    Private _pData As MobData
    Private _currentZone As Byte
    Private _zoneName As String = "Unknown"


    'Private _targetId As Integer
    Private _lastTargetId As Integer = -1
    Private _tData As MobData

    Private _tsOverlayCMS As ContextMenuStrip
    Private _tsMappedCMS As ContextMenuStrip

    Private _isProcessSelected As Boolean
    Private _currentProcessID As Integer

    Private _isLoading As Boolean
    Private _lostFocusCount As Integer

    Private _isMobCamped As Boolean

    Private _pList As IEnumerable(Of Process)

    'Watchers
    Private WithEvents _mobWatcher As Watcher

#End Region

#Region " DELEGATE "
    Private Delegate Sub MobSpawnedCallback(ByVal mob As MobData, ByVal OutofRange As Boolean)
#End Region

#Region " PROPERTIES "
    Public ReadOnly Property BrowserForm() As BrowserForm
        Get
            If _bf Is Nothing OrElse _bf.IsDisposed Then
                _bf = New BrowserForm
            End If
            Return _bf
        End Get
    End Property

    Public ReadOnly Property DataForm() As DataBrowserForm
        Get
            If _dbf Is Nothing OrElse _dbf.IsDisposed Then
                _dbf = New DataBrowserForm
            End If
            Return _dbf
        End Get
    End Property

    Private _zones As FFXIMemory.Zones
    Private ReadOnly Property Zones() As FFXIMemory.Zones
        Get
            If _zones Is Nothing Then
                _zones = New FFXIMemory.Zones
            End If
            Return _zones
        End Get
    End Property

    Private WithEvents _zoneTimer As Stopwatch
    Private ReadOnly Property ZoneTimer() As Stopwatch
        Get
            If _zoneTimer Is Nothing Then
                _zoneTimer = New Stopwatch
            End If
            Return _zoneTimer
        End Get
    End Property
    Private Property CampingMode() As Boolean
    Private Property PointerBlock() As Byte()

    Private _mobs As List(Of MobData)
    Private Property Mobs As List(Of MobData)
        Get
            If _mobs Is Nothing Then
                _mobs = New List(Of MobData)
            End If
            Return _mobs
        End Get
        Set(ByVal value As List(Of MobData))
            _mobs = value
        End Set
    End Property

    Private Property SpawnWatching As Boolean

    Private _spawnList As List(Of Integer)
    Friend Property SpawnList As List(Of Integer)
        Get
            If _spawnList Is Nothing Then
                _spawnList = New List(Of Integer)
            End If
            Return _spawnList
        End Get
        Set(ByVal value As List(Of Integer))
            _spawnList = value
        End Set
    End Property

    Private _watchList As List(Of Integer)
    Friend Property WatchList As List(Of Integer)
        Get
            If _watchList Is Nothing Then
                _watchList = New List(Of Integer)
            End If
            Return _watchList
        End Get
        Set(ByVal value As List(Of Integer))
            _watchList = value
        End Set
    End Property

    Private _trackForms As List(Of SpawnTrackerForm)
    Private ReadOnly Property TrackForms As List(Of SpawnTrackerForm)
        Get
            If _trackForms Is Nothing Then
                _trackForms = New List(Of SpawnTrackerForm)
            End If
            Return _trackForms
        End Get
    End Property

    Public Property Scanner As Scanner
#End Region

#Region " FORM ACTIONS "
    Private Sub AppBarForm_Activated(ByVal sender As Object, ByVal e As System.EventArgs) Handles Me.Activated
        If Not NoBar Then
            BringToFront()
        End If
    End Sub

    Private Sub Form1_FormClosing(ByVal sender As Object, ByVal e As System.Windows.Forms.FormClosingEventArgs) Handles Me.FormClosing
        Scanner.ScannerSingleton.DetachWatcher(_mobWatcher)

        'Opacity = 1
        If Not _sof Is Nothing Then
            _sof.FadeOut(1000)
        End If
        If _abm IsNot Nothing Then
            _abm.UnregisterAppBar()
        End If
        CampedMobManager.SaveData()
        'SetWindowText(_ffxi.POL.MainWindowHandle, _pData.Name)
    End Sub

    Private Sub AppBarForm_Shown(ByVal sender As Object, ByVal e As System.EventArgs) Handles Me.
        Shown
        If NoBar Then
            Hide()
        End If
    End Sub

    Private Sub Form1_Load(ByVal sender As System.Object, ByVal e As System.EventArgs) Handles MyBase.Load
        Try
            ApplyTheme()
            _isLoading = True

            If Not DataAccess.MobData.IsInitialized Then
                DataAccess.MobData.BeginInit()
            End If
            'DataAccess.MobData.AcceptChanges()

            'Setup the menus
            CloseMenu.Renderer = New Office2007Renderer
            MainMenu.Renderer = New Office2007Renderer

            For Each tsi As ToolStripItem In tsApRadar.DropDownItems
                If tsi.Tag = "pro" Then
                    tsi.Enabled = GlobalSettings.IsProEnabled
                End If
            Next

            For Each tsi As ToolStripItem In NotifyMenu.Items
                If tsi.Tag = "pro" Then
                    tsi.Enabled = GlobalSettings.IsProEnabled
                End If
            Next


            tsPosition.Visible = My.Settings.ShowPosition
            positionSpacer.Visible = My.Settings.ShowPosition

            tsClock.Visible = My.Settings.ShowClock
            clockSpacer.Visible = My.Settings.ShowClock

            tsZone.Visible = My.Settings.ShowZoneTimer
            zoneSpacer.Visible = My.Settings.ShowZoneTimer

            If GlobalSettings.IsProEnabled Then
                If GlobalSettings.EliteMMOUser Then
                    tsActivatePro.Text = "EliteMMO User"
                    ntsActivatePro.Text = "EliteMMO User"
                    ntsActivatePro.Enabled = False
                    tsActivatePro.Enabled = False
                Else
                    tsActivatePro.Text = "Deactivate Pro"
                    ntsActivatePro.Text = "Deactivate Pro"
                End If
                tsCampingMode.Checked = My.Settings.CampingMode
                ntsCampingMode.Checked = My.Settings.CampingMode
                tsSpawnAlerts.Checked = My.Settings.StartSpawnAlerts
                'Load the camped mobs data
                CampedMobManager.LoadData()
                'Load the global watch list for spawn alerts
                LoadWatchList()
            Else
                tsActivatePro.Text = "Activate Pro"
                ntsActivatePro.Text = "Activate Pro"
            End If


            'Set the renderers
            If Not NoBar Then
                LoadRSS()
                If My.Settings.AutomaticNewsUpdates Then
                    NewsTimer.Interval = (My.Settings.NewsCheckInterval * 60 * 1000)
                    NewsTimer.Start()
                End If
                ntsSelectProcess.Visible = False
                ProcessSeperator.Visible = False
                'The mob dataform
                _mdf = New MobDataForm(Me)
                _pcf = New PCInfoForm
                Dim mt As MonitorType
                If My.Settings.Monitor = "Secondary" AndAlso Screen.AllScreens.Count > 1 Then
                    If Screen.AllScreens(1) Is Screen.PrimaryScreen Then
                        _screen = Screen.AllScreens(0)
                    Else
                        _screen = Screen.AllScreens(1)
                    End If
                    mt = MonitorType.Secondary
                Else
                    _screen = Screen.PrimaryScreen
                    mt = MonitorType.Primary
                End If

                Try
                    _abm = New AppBarManager(Me, ThemeHandler.ActiveTheme.DockPosition, mt, _screen.Bounds.Width, 36)
                Catch ex As Exception
                    _abm = New AppBarManager(Me, DockMode.Top, mt, 100, 36)
                End Try

                tsAutoHideBar.Checked = My.Settings.AutoHide
                If Not My.Settings.AutoHide Then
                    _abm.InitializeAppBar()
                Else
                    Location = New Point(_screen.Bounds.Left, _screen.Bounds.Top)
                    Width = _screen.Bounds.Width
                    _isAutoHide = True
                    If _timer Is Nothing Then
                        _timer = New Timer
                    End If
                    _timer.Interval = 100
                    _timer.Start()
                End If

                _sof = New FormAnimator(Me)
                _sof.FadeIn(1000)



            Else
                Size = New Size(0, 0)
                ntsAutoHide.Visible = False
                _screen = Screen.PrimaryScreen
            End If
            NotifyMenu.Renderer = New Office2007Renderer

            _mobWatcher = New Watcher(MemoryScanner.WatcherTypes.MobList Or MemoryScanner.WatcherTypes.ZoneChange)
            Scanner.ScannerSingleton.AttachWatcher(_mobWatcher)

            LoadAvailableProcesses()
            'processWatchTimer.Start()
            If My.Settings.MapOpen Then
                tsMapped.PerformClick()
            End If
            If My.Settings.OverlayOpen Then
                tsOverlay.PerformClick()
            End If

            If DebugRun Then
                _debugForm = New DebugForm
                _debugForm.Show()
            End If

            AddHandler CampedMobManager.DebugEvent, AddressOf CampedMobManager_DebugEvent
#If DEBUG Then
            TestRadarToolStripMenuItem.Visible = True
#End If
        Catch ex As Exception
            MessageBox.Show(ex.Message)
        End Try
        _isLoading = False
    End Sub
#End Region

#Region " OVERRIDE METHODS "
    Protected Overrides ReadOnly Property CreateParams() As System.Windows.Forms.CreateParams
        Get
            Dim cp As CreateParams = MyBase.CreateParams
            cp.ExStyle = cp.ExStyle Or &H80
            cp.ExStyle = cp.ExStyle And Not &H40000
            Return cp
        End Get
    End Property
#End Region

#Region " CONTROLS "
#Region " -- APRADAR MENU "
    Private Sub tsOverlay_Click(ByVal sender As System.Object, ByVal e As System.EventArgs) Handles tsOverlay.Click, ntsOverlayRadar.Click
        'If Not _ffxi Is Nothing Then
        If _orf Is Nothing OrElse _orf.IsDisposed Then
            _orf = New OverlayRadarForm(_ffxi)
        End If
        _orf.ProEnabled = GlobalSettings.IsProEnabled
        _orf.OverlayRadar.TextRendering = My.Settings.TextRendering
        _orf.OverlayRadar.SmoothingMode = My.Settings.SmoothingMode
        _orf.OverlayRadar.CompositingQuality = My.Settings.CompositingQuality
        tsOverlayRadar.Visible = True
        _orf.Show()
        'End If
    End Sub

    Private Sub tsMapped_Click(ByVal sender As System.Object, ByVal e As System.EventArgs) Handles tsMapped.Click, ntsMapRadar.Click
        'If Not _ffxi Is Nothing Then
        If _mrf Is Nothing OrElse _mrf.IsDisposed Then
            _mrf = New MappedRadarForm()
        End If
        _mrf.ProEnabled = GlobalSettings.IsProEnabled
        _mrf.MapRadar.TextRendering = My.Settings.TextRendering
        _mrf.MapRadar.SmoothingMode = My.Settings.SmoothingMode
        _mrf.MapRadar.CompositingQuality = My.Settings.CompositingQuality
        tsMappedRadar.Visible = True
        _mrf.Show()
        'End If
    End Sub

    Private Sub tsRecipeSearch_Click(ByVal sender As System.Object, ByVal e As System.EventArgs) Handles tsRecipeSearch.Click, ntsRecipeSearch.Click
        Dim rsf As New RecipeSearchForm(_ffxi)
        rsf.HeaderPanel.BackgroundImage = ThemeHandler.HeaderImage

        rsf.Left = _screen.Bounds.Left + 110
        If _abm Is Nothing Then
            rsf.Top = 0
        ElseIf _abm.DockMode = DockMode.Top Then
            rsf.Top = Height
        Else
            rsf.Top = (Location.Y - rsf.Height)
        End If
        rsf.Show()
    End Sub

    Private Sub tsDatabase_Click(ByVal sender As System.Object, ByVal e As System.EventArgs) Handles tsDatabase.Click, ntsDatabase.Click
        DataForm.Left = _screen.Bounds.Left
        If _abm Is Nothing Then
            DataForm.Top = 0
        ElseIf _abm.DockMode = DockMode.Top Then
            DataForm.Top = Height
        Else
            DataForm.Top = Top - DataForm.Height
        End If
        DataForm.CurrentZone = _currentZone
        DataForm.Show()
        DataForm.cboZones.SelectedValue = _currentZone
    End Sub

    Private Sub tsAbout_Click(ByVal sender As System.Object, ByVal e As System.EventArgs) Handles tsAbout.Click, ntsAboutApRadar.Click
        Dim abf As New AboutForm
        abf.Show()
    End Sub

    Private Sub tsCampedMobBrowser_Click(ByVal sender As System.Object, ByVal e As System.EventArgs) Handles tsCampedMobBrowser.Click, ntsViewTod.Click
        If Not _orf Is Nothing AndAlso Not _orf.IsDisposed Then
            _orf.OverlayRadar.SaveCampedMobs()
        End If
        Dim tod As New TodBrowserForm(_currentZone) With {.Left = Me.Left}
        If _abm Is Nothing Then
            tod.Top = 0
        ElseIf _abm.DockMode = DockMode.Top Then
            tod.Top = 36
        Else
            tod.Top = Top - tod.Height
        End If
        tod.Show()
    End Sub

    Private Sub tsSettings_Click(ByVal sender As System.Object, ByVal e As System.EventArgs) Handles tsSettings.Click, ntsSettings.Click
        Dim sf As New SettingsForm
        AddHandler sf.SettingsChanged, AddressOf SettingsForm_SettingsChanged
        sf.Show()
    End Sub

    Private Sub SettingsForm_SettingsChanged()
        ApplySettings()
    End Sub

    Private Sub tsActivatePro_Click(ByVal sender As System.Object, ByVal e As System.EventArgs) Handles tsActivatePro.Click, ntsActivatePro.Click
        If tsActivatePro.Text = "Activate Pro" Then
            Using apd As New ActivateProDialog()
                apd.ShowDialog()
            End Using
        Else
            If MessageBox.Show("This will deactivate the Pro features on this computer.  To continue click OK, otherwise click Cancel to abort", "Disable Pro?", MessageBoxButtons.OKCancel, MessageBoxIcon.Question) = Windows.Forms.DialogResult.OK Then
                Try
                    Using vs As New WebService.ValidationServicePortTypeClient()
                        If vs.Deactivate(GetComputerKey, GlobalSettings.LicenseUser) Then
                            GlobalSettings.IsProEnabled = False
                            GlobalSettings.LicenseUser = String.Empty
                            GlobalSettings.ActivationCount = 0
                            GlobalSettings.ExpirationDate = Nothing
                            If IO.File.Exists(LicensePath) Then
                                IO.File.Delete(LicensePath)
                            End If
                            MessageBox.Show("Computer successfully deactivated", "Deactivation Complete", MessageBoxButtons.OK, MessageBoxIcon.Information)
                        Else
                            MessageBox.Show("There was an error deactivating this computer.  If the problem persists, please contact support")
                        End If
                    End Using
                Catch ex As Exception
                    MessageBox.Show("Unable to deactivate this computer at this time, Please try again later or contact support")
                End Try
            End If
        End If

        For Each tsi As ToolStripItem In tsApRadar.DropDownItems
            If tsi.Tag = "pro" Then
                tsi.Enabled = GlobalSettings.IsProEnabled
            End If
        Next

        For Each tsi As ToolStripItem In NotifyMenu.Items
            If tsi.Tag = "pro" Then
                tsi.Enabled = GlobalSettings.IsProEnabled
            End If
        Next

        If Not _orf Is Nothing AndAlso Not _orf.IsDisposed Then
            _orf.ProEnabled = GlobalSettings.IsProEnabled
        End If
        If Not _mrf Is Nothing AndAlso Not _mrf.IsDisposed Then
            _mrf.ProEnabled = GlobalSettings.IsProEnabled
        End If
        If GlobalSettings.IsProEnabled Then
            tsActivatePro.Text = "Deactivate Pro"
        Else
            tsActivatePro.Text = "Activate Pro"
        End If
    End Sub

    Private Sub tsMapUpdateCheck_Click(ByVal sender As System.Object, ByVal e As System.EventArgs) Handles tsMapUpdateCheck.Click, ntsMapUpdates.Click
        CheckMapVersion(True)
    End Sub

    Private Sub tsCheckForUpdates_Click(ByVal sender As System.Object, ByVal e As System.EventArgs) Handles tsCheckForUpdates.Click, ntsUpdates.Click
        If Not CheckApplicationVersion() Then
            MessageBox.Show("You are running the latest version of ApRadar", "No Available Updates", MessageBoxButtons.OK)
        Else
            Close()
        End If
    End Sub

    Private Sub ntsAutoHide_CheckedChanged(ByVal sender As Object, ByVal e As System.EventArgs) Handles ntsAutoHide.CheckedChanged
        tsAutoHideBar.Checked = ntsAutoHide.Checked
    End Sub

    Private Sub tsAutoHideBar_CheckedChanged(ByVal sender As Object, ByVal e As System.EventArgs) Handles tsAutoHideBar.CheckedChanged
        If Not _isLoading AndAlso tsAutoHideBar.Checked <> _isAutoHide Then
            If tsAutoHideBar.Checked Then
                If Not _abm Is Nothing Then
                    _abm.UnregisterAppBar()
                End If
                _isAutoHide = True
                If _timer Is Nothing Then
                    _timer = New Timer
                End If
                _timer.Interval = 100
                _timer.Start()
            Else
                _timer.Stop()
                If Not _abm Is Nothing Then
                    _abm.DockMode = ThemeHandler.ActiveTheme.DockPosition
                    _abm.InitializeAppBar()
                End If
                _sof.SlideOut(150, SlideDirection.Down)
                _isAutoHide = False
            End If
            ntsAutoHide.Checked = tsAutoHideBar.Checked
        End If
    End Sub

    Private Sub ExitToolStripMenuItem_Click(ByVal sender As System.Object, ByVal e As System.EventArgs) Handles ExitToolStripMenuItem.Click, ntsExit.Click
        tsExit.PerformClick()
    End Sub

    Private Sub tsCampingMode_CheckedChanged(ByVal sender As Object, ByVal e As System.EventArgs) Handles tsCampingMode.CheckedChanged
        CampingMode = tsCampingMode.Checked
        ntsCampingMode.Checked = tsCampingMode.Checked
    End Sub

    Private Sub ntsCampingMode_CheckedChanged(ByVal sender As Object, ByVal e As System.EventArgs) Handles ntsCampingMode.CheckedChanged
        tsCampingMode.Checked = ntsCampingMode.Checked
    End Sub

    Private Sub tsSpawnAlerts_CheckedChanged(ByVal sender As Object, ByVal e As System.EventArgs) Handles tsSpawnAlerts.CheckedChanged
        SpawnWatching = tsSpawnAlerts.Checked
        ntsSpawnAlerts.Checked = tsSpawnAlerts.Checked
        'LEt the watcher know that it should watch for spawns too
        If SpawnWatching Then
            If _mobWatcher.Type And MemoryScanner.WatcherTypes.MobStatus <> MemoryScanner.WatcherTypes.MobStatus Then
                _mobWatcher.Type = _mobWatcher.Type Or MemoryScanner.WatcherTypes.MobStatus
            End If
        Else
            _mobWatcher.Type = _mobWatcher.Type Xor MemoryScanner.WatcherTypes.MobStatus
        End If

    End Sub

    Private Sub ntsSpawnAlerts_CheckedChanged(ByVal sender As Object, ByVal e As System.EventArgs) Handles ntsSpawnAlerts.CheckedChanged
        tsSpawnAlerts.Checked = ntsSpawnAlerts.Checked
    End Sub

    Private Sub tsSpawnFilter_Click(ByVal sender As System.Object, ByVal e As System.EventArgs) Handles tsSpawnFilter.Click, ntsSpawnFilter.Click
        Using sad As New SpawnAlertDialog(_currentZone) With {.SpawnList = SpawnList, .GlobalWatch = WatchList}
            If sad.ShowDialog = DialogResult.OK Then
                SpawnList = sad.SpawnList
                WatchList = sad.GlobalWatch
                SaveWatchList()
            End If
        End Using
    End Sub

    Private Sub tsChat_Click(ByVal sender As System.Object, ByVal e As System.EventArgs) Handles tsChat.Click, ntsApRadarChat.Click
        Dim cf As New ChatForm
        cf.Show()
    End Sub

    Private Sub FFXIRemoteChatServToolStripMenuItem_Click(ByVal sender As System.Object, ByVal e As System.EventArgs) Handles tsFFXIRemoteChat.Click, ntsFFXIChatServer.Click
        If Not _ffxi Is Nothing Then
            If My.Settings.RemoteChatMessage Then
                Using mf As New RemoteChatMessageDialog()
                    mf.ShowDialog()
                End Using
            End If
            Dim rcs As New RemoteChatServerForm(_ffxi)
            rcs.Show()
        Else
            MessageBox.Show("FFXI Must be running and you must have an instance selected to use the Remote Chat Feature")
        End If
    End Sub
#End Region

#Region " -- MAIN MENU BAR "
    Private Sub txtMob_KeyPress(ByVal sender As Object, ByVal e As System.Windows.Forms.KeyPressEventArgs) Handles txtSearch.KeyPress
        If e.KeyChar = Chr(Keys.Enter) Then
            e.Handled = True
        End If
    End Sub

    Private Sub txtMob_KeyUp(ByVal sender As System.Object, ByVal e As System.Windows.Forms.KeyEventArgs) Handles txtSearch.KeyUp
        If e.KeyCode = Keys.Enter Then
            If Not String.IsNullOrEmpty(txtSearch.Text.Trim) Then
                tsSearch.PerformClick()
            End If
        End If
    End Sub

    Private Sub tsSearch_Click(ByVal sender As System.Object, ByVal e As System.EventArgs) Handles tsSearch.Click
        If txtSearch.Text.Trim <> String.Empty Then
            Dim targetHeight As Integer
            Dim searchUrl As String = String.Empty

            'Get the search url
            searchUrl = GetSearchUrl(CType(tsSearchProvider.Tag, SearchProvider), txtSearch.Text)
            If searchUrl = "Recipes" OrElse searchUrl = "Items" Then
                If searchUrl = "Recipes" Then
                    Dim rf As New RecipeSearchForm(_ffxi)
                    rf.SearchItem(txtSearch.Text, RecipeSearchForm.SearchType.Result)
                    rf.Show()
                End If
            Else
                BrowserForm.Browser.Navigate(searchUrl)
                If Not BrowserForm.Visible Then

                    BrowserForm.Width = Math.Min(1024, _screen.Bounds.Width - 106)
                    targetHeight = _screen.Bounds.Height - 236
                    If targetHeight < 800 Then
                        BrowserForm.Height = _screen.Bounds.Height - 236
                    Else
                        BrowserForm.Height = 800
                    End If
                    If ThemeHandler.ActiveTheme.DockPosition = DockMode.Top Then
                        BrowserForm.Location = New Point(_screen.Bounds.Left + 106, Height)
                    Else
                        BrowserForm.Location = New Point(_screen.Bounds.Left + 106, Top - BrowserForm.Height)
                    End If
                    BrowserForm.Opacity = Opacity
                    BrowserForm.Show()
                End If
            End If
        End If
    End Sub

    Private Sub tsAddNewMob_Click(ByVal sender As System.Object, ByVal e As System.EventArgs)
        Dim index = DataAccess.AddMob(_tData.Name, _mdf.Zone, False)
        _mdf.MobID = index
        If index > 0 Then
            tsAddMob.Visible = False
        End If
    End Sub

    Private Sub tsAddMob_Click(ByVal sender As Object, ByVal e As System.EventArgs) Handles tsAddMob.Click
        Dim index = DataAccess.AddMob(_tData.Name, _mdf.Zone, True)
        _mdf.MobID = index
        If index > 0 Then
            tsAddMob.Visible = False
        End If
    End Sub
#End Region

#Region " -- CLOSE MENU "
    Private Sub tsExit_Click(ByVal sender As System.Object, ByVal e As System.EventArgs) Handles tsExit.Click
        If Not BrowserForm Is Nothing AndAlso BrowserForm.Visible Then
            BrowserForm.Close()
        End If
        If Not _mrf Is Nothing AndAlso _mrf.Visible Then
            My.Settings.MapOpen = True
            _mrf.Close()
        Else
            My.Settings.MapOpen = False
        End If
        If Not _orf Is Nothing AndAlso _orf.Visible Then
            My.Settings.OverlayOpen = True
            _orf.Close()
        Else
            My.Settings.OverlayOpen = False
        End If
        Close()
    End Sub

    Private Sub tsOverlayRadar_Click(ByVal sender As System.Object, ByVal e As System.EventArgs) Handles tsOverlayRadar.Click
        If Not _orf Is Nothing AndAlso Not _orf.IsDisposed Then
            _orf.BringToFront()
            _tsOverlayCMS = _orf.OverlayRadar.ContextMenuStrip
            If _tsOverlayCMS.Visible Then
                _tsOverlayCMS.Hide()
            Else
                _tsOverlayCMS.Show(MainMenu, New Point(tsOverlayRadar.Bounds.Left, 36))
            End If
        End If
    End Sub

    Private Sub tsMappedRadar_Click(ByVal sender As Object, ByVal e As System.EventArgs) Handles tsMappedRadar.Click
        If Not _mrf Is Nothing AndAlso Not _mrf.IsDisposed Then
            _mrf.BringToFront()
            _tsMappedCMS = _mrf.MapRadar.ContextMenuStrip
            If _tsMappedCMS.Visible Then
                _tsMappedCMS.Hide()
            Else

                _tsMappedCMS.Show(MainMenu, New Point(tsMappedRadar.Bounds.Left, 36))
            End If
        End If
    End Sub

    Private Sub RSS_LinkClicked(ByVal sender As System.Object, ByVal e As System.Windows.Forms.LinkLabelLinkClickedEventArgs)
        Dim url As String = String.Empty
        If TypeOf sender Is LinkLabel Then
            url = CType(sender, LinkLabel).Tag
        ElseIf TypeOf sender Is Label Then
            url = CType(CType(sender, Label).Parent, FeedViewer).lnkTitle.Tag
        ElseIf TypeOf sender Is FeedViewer Then
            url = CType(sender, FeedViewer).lnkTitle.Tag
        End If
        BrowserForm.Browser.Navigate(url)
        If Not BrowserForm.Visible Then
            Dim targetHeight As Integer
            BrowserForm.Width = Math.Min(1024, _screen.Bounds.Width - 106)
            targetHeight = _screen.Bounds.Height - 236
            If targetHeight < 800 Then
                BrowserForm.Height = _screen.Bounds.Height - 236
            Else
                BrowserForm.Height = 800
            End If
            If ThemeHandler.ActiveTheme.DockPosition = DockMode.Top Then
                BrowserForm.Location = New Point(_screen.Bounds.Left + 106, Height)
            Else
                BrowserForm.Location = New Point(_screen.Bounds.Left + 106, Top - BrowserForm.Height)
            End If
            BrowserForm.Opacity = Opacity
            BrowserForm.Show()
        End If
    End Sub

    Private Sub tsProcess_DropDownOpening(ByVal sender As System.Object, ByVal e As System.EventArgs) Handles tsProcess.DropDownOpening
        LoadAvailableProcesses()
    End Sub
#End Region

#Region " -- FORM CONTROLS "
    Private Sub AppBarForm_MouseEnter(ByVal sender As Object, ByVal e As System.EventArgs) Handles Me.MouseEnter, MainMenu.MouseEnter, CloseMenu.MouseEnter
        SetActiveWindow(Handle)
    End Sub

    Private Sub tsAlla_Click(ByVal sender As System.Object, ByVal e As System.EventArgs) Handles tsAlla.Click, tsWiki.Click, tsItems.Click, tsRecipes.Click, tsFFXIAH.Click
        tsSearchProvider.Image = CType(sender, ToolStripItem).Image
        tsSearchProvider.Tag = sender.Tag
        tsSearchProvider.ToolTipText = String.Format("Search using {0}", sender.Text)
    End Sub

    Private Sub tsTargetInfo_MouseEnter(ByVal sender As Object, ByVal e As System.EventArgs) Handles tsTargetInfo.MouseEnter
        Try
            If _mdf.MobFound Then
                _isTargetInfoDropped = True
                _mdf.Left = _screen.Bounds.Left + 320
                If _abm.DockMode = DockMode.Top Then
                    _mdf.Top = Height
                Else
                    _mdf.Top = (Location.Y - _mdf.Height)
                End If
                _mdf.BringToFront()
                _mdf.SlideOut()
                MouseOverTimer.Start()
            ElseIf _pcf.PCFound Then
                _isPCInfoDropped = True
                _pcf.Left = _screen.Bounds.Left + 320
                If _abm.DockMode = DockMode.Top Then
                    _pcf.Top = Height
                Else
                    _pcf.Top = (Location.Y - _pcf.Height)
                End If
                _pcf.BringToFront()
                _pcf.RollOut()
                MouseOverTimer.Start()
            End If
        Catch

        End Try
    End Sub

    Private Sub tsRSS_DropDownClosed(ByVal sender As Object, ByVal e As System.EventArgs) Handles tsRSS.DropDownClosed
        tsRSS.Image = My.Resources.Rss
    End Sub

    Private Sub CheckForNewNewsItemsToolStripMenuItem_Click(ByVal sender As System.Object, ByVal e As System.EventArgs) Handles CheckForNewNewsItemsToolStripMenuItem.Click
        LoadRSS()
    End Sub

    Private Sub ScanDatsForNewMobsToolStripMenuItem_Click(ByVal sender As System.Object, ByVal e As System.EventArgs) Handles ScanDatsForNewMobsToolStripMenuItem.Click
        Dim dcd As New DatCheckDialog
        dcd.Show()
    End Sub

    Private Sub TestRadarToolStripMenuItem_Click(ByVal sender As System.Object, ByVal e As System.EventArgs) Handles TestRadarToolStripMenuItem.Click
        ShowToastForm("Message", True)
    End Sub
#End Region
#End Region

#Region " TIMERS "

    Private Sub _timer_tick(ByVal sender As Object, ByVal e As EventArgs) Handles _timer.Tick
        If Not _isAutoHide Then
            _timer.Stop()
        Else
            If _isHidden Then
                If ThemeHandler.ActiveTheme.DockPosition = DockMode.Top Then
                    If Cursor.Position.X >= _screen.Bounds.Left AndAlso Cursor.Position.X <= _screen.Bounds.Right AndAlso Cursor.Position.Y <= 5 Then
                        _isHidden = False
                        BringToFront()
                        _sof.SlideOut(150, SlideDirection.Down)
                    End If
                Else
                    If Cursor.Position.X >= _screen.Bounds.Left AndAlso Cursor.Position.X <= _screen.Bounds.Right AndAlso Cursor.Position.Y >= Bounds.Bottom - 5 AndAlso Cursor.Position.Y <= Bounds.Bottom Then
                        _isHidden = False
                        BringToFront()
                        _sof.SlideOut(150, SlideDirection.Up)
                    End If
                End If
            Else
                If ThemeHandler.ActiveTheme.DockPosition = DockMode.Top Then
                    If Not Bounds.Contains(Cursor.Position) Then
                        If (BrowserForm Is Nothing OrElse Not BrowserForm.Visible) AndAlso _
                        Not tsApRadar.DropDown.Visible AndAlso Not tsSearchProvider.DropDown.Visible _
                        AndAlso Not _isTargetInfoDropped AndAlso Not _isPCInfoDropped AndAlso (_tsOverlayCMS Is Nothing OrElse Not _tsOverlayCMS.Visible) _
                        AndAlso _lostFocusCount >= 10 Then
                            _lostFocusCount = 0
                            _isHidden = True
                            _sof.RollUp(150, SlideDirection.Down)
                        Else
                            _lostFocusCount += 1
                        End If
                    Else
                        _lostFocusCount = 0
                    End If
                Else
                    If Not Bounds.Contains(Cursor.Position) Then
                        If (BrowserForm Is Nothing OrElse Not BrowserForm.Visible) AndAlso _
                        Not tsApRadar.DropDown.Visible AndAlso Not tsSearchProvider.DropDown.Visible _
                        AndAlso Not _isTargetInfoDropped AndAlso (_tsOverlayCMS Is Nothing OrElse Not _tsOverlayCMS.Visible) _
                        AndAlso _lostFocusCount >= 10 Then
                            _lostFocusCount = 0
                            _isHidden = True
                            _sof.RollUp(150, SlideDirection.Up)
                        Else
                            _lostFocusCount += 1
                        End If
                    Else
                        _lostFocusCount = 0
                    End If
                End If
            End If
        End If
    End Sub

    Private Sub _targetTimer_tick(ByVal sender As Object, ByVal e As EventArgs) Handles _targetTimer.Tick
        ''Check to make sure the FFXI object exists, that a process has been selected
        ''and that the game is loaded
        'If Not _ffxi Is Nothing AndAlso _isProcessSelected AndAlso _ffxi.IsGameLoaded Then
        '    'When loading if pol is not at the state of in game
        '    'we will get an error here so we put all of this in a 
        '    'try catch to handle any exceptions.  The exceptions will 
        '    'be ignored and we will just continue until the game is in the
        '    'ready state.
        '    Try
        '        'Check memory object and create if necessry
        '        'This will be nothing on initial load and also after a new process
        '        'is selected
        '        If Not NoBar Then
        '            If _mem Is Nothing Then
        '                _mem = New Memory(_ffxi.POL, 0)
        '            End If
        '            _mem.Address = _ffxi.MemLocs("MAPBASE")
        '            _currentZone = _mem.GetByte
        '            If _currentZone <> _lastZone Then
        '                Mobs.Clear()
        '                SpawnList.Clear()
        '                _lastZone = _currentZone
        '                _zoneName = Zones.GetZoneName(_currentZone)
        '                ZoneTimer.Reset()
        '                ZoneTimer.Start()
        '            End If
        '            'Get My Info
        '            'Set address to OWNPOS
        '            _mem.Address = _ffxi.MemLocs("OWNPOSITION")
        '            'Read my id
        '            _myId = _mem.GetInt32
        '            'Set address for NPCMAP
        '            _mem.Address = _ffxi.MemLocs("NPCMAP") + (4 * _myId)
        '            'Get Mob array base for my entry
        '            _mobArrayBase = _mem.GetInt32
        '            'Verify that we have found our ID.  
        '            'If not found we will exit the loop and just set the Memory object to nothing
        '            If _mobArrayBase <> 0 AndAlso _myId <> 0 Then

        '                'Read my position data form the mob structure
        '                _pData.MobBase = _mobArrayBase
        '                'Write the output to the tool strip
        '                If My.Settings.ShowPosition Then
        '                    tsPosition.Text = String.Format("X: {0} Y: {1} Z: {2} [{3}° {4}]", _pData.X.ToString("0.000"), _
        '                                                       _pData.Y.ToString("0.000"), _pData.Z.ToString("0.000"), _
        '                                                       Math.Round(RadiansToDegrees(_pData.Direction), 2), GetHeading(_pData.Direction))
        '                End If

        '                'Now we will get Target Info
        '                'Set the memory objects adrress to the targetinfo pointer
        '                _mem.Address = _ffxi.MemLocs("TARGETINFO")
        '                'Read the target's id pointer
        '                _targetId = _mem.GetInt32
        '                'Set the memory object address again
        '                _mem.Address = _targetId
        '                'Get the current target's id
        '                _targetId = _mem.GetShort
        '                'If we have a target selected, we will continue with processing
        '                If _targetId > 0 Then
        '                    'set the address to the pointer to the targets mob array entry
        '                    _mem.Address = _ffxi.MemLocs("NPCMAP") + (4 * _targetId)
        '                    'Read the pointer
        '                    _targetBase = _mem.GetInt32
        '                    'Set the target data object base address to the found address
        '                    _tData.MobBase = _targetBase
        '                    'Handle the pc entries
        '                    If _tData.MobType = MobTypes.PC Then
        '                        'LightSteelBlue is the standatrd
        '                        tsTargetInfo.ForeColor = ThemeHandler.PCColor
        '                        tsTargetInfo.Text = String.Format("PC [0x{0}] {1} - HP: {2}% ", _targetId.ToString("X"), _
        '                                                             _tData.Name, _tData.HP)
        '                        If _targetId <> _lastTargetId Then
        '                            _lastTargetId = _targetId
        '                        End If

        '                        If DataAccess.AddPC(_tData.Name, _tData.ServerID) Then
        '                            _pcf.PCID = _tData.ServerID
        '                        Else
        '                            _pcf.PCID = -1
        '                        End If
        '                        _mdf.MobID = -1
        '                        tsAddMob.Visible = False
        '                    Else 'Handle any npc entries
        '                        'Tomato is the standard
        '                        tsTargetInfo.ForeColor = ThemeHandler.NPCColor
        '                        tsTargetInfo.Text = String.Format("NPC [0x{0}] {1} HP: {2}% ", _targetId.ToString("X"), _
        '                                                             _tData.Name, _tData.HP)
        '                        If _targetId <> _lastTargetId Then
        '                            '_targetInfoData = BuildMobInfo()
        '                            _mdf.Zone = _currentZone
        '                            Dim name As String = _tData.Name
        '                            _mob = (From mob In DataAccess.MobData.Mobs _
        '                                    Where mob.MobName = name And mob.Zone = _mdf.Zone).FirstOrDefault
        '                            If _mob Is Nothing Then
        '                                tsAddMob.Visible = True
        '                                _mdf.MobID = -1
        '                            Else
        '                                tsAddMob.Visible = False
        '                                _mdf.MobID = _mob.MobPK
        '                            End If
        '                            _lastTargetId = _targetId
        '                            _pcf.PCID = -1

        '                        End If


        '                    End If
        '                Else
        '                    _mdf.MobID = -1
        '                    _pcf.PCID = -1
        '                    tsAddMob.Visible = False
        '                    tsTargetInfo.ForeColor = MainMenu.ForeColor
        '                    tsTargetInfo.Text = "No Target"
        '                    _targetInfoData = String.Empty

        '                End If
        '                'If _MouseOverTimer.Enabled AndAlso Not _mdf.MobFound AndAlso Not _isPCInfoDropped Then
        '                '    _MouseOverTimer.Stop()
        '                '    _mdf.RollUp()
        '                'End If
        '                'If _MouseOverTimer.Enabled AndAlso _pcf.PCFound AndAlso Not _isTargetInfoDropped Then
        '                '    _MouseOverTimer.Stop()
        '                '    _pcf.RollUp()
        '                'End If
        '            End If
        '            'If CampingMode OrElse SpawnWatching Then
        '            '    Dim t As New Threading.Thread(AddressOf MobScanner)
        '            '    t.Start()
        '            'End If
        '        Else
        '            _mem = Nothing
        '        End If
        '    Catch ex As Exception

        '    End Try
        'End If
    End Sub

    Private Sub CurrentZoneTimer_Tick(ByVal sender As System.Object, ByVal e As System.EventArgs) Handles CurrentZoneTimer.Tick
        If My.Settings.ShowZoneTimer Then
            tsZone.Text = String.Format("Zone: [{0}] {1} - {2}:{3}:{4}", _currentZone.ToString("X2"), _zoneName, ZoneTimer.Elapsed.Hours.ToString("00"), ZoneTimer.Elapsed.Minutes.ToString("00"), ZoneTimer.Elapsed.Seconds.ToString("00"))
        End If
        If My.Settings.ShowClock Then
            tsClock.Text = Date.Now.ToLongTimeString
        End If
    End Sub

    Private Sub MouseOverTimer_tick(ByVal sender As Object, ByVal e As System.EventArgs) Handles MouseOverTimer.Tick
        Dim cursorLoc As Point = Cursor.Position
        Dim textRect As New Rectangle(_screen.Bounds.Left + tsTargetInfo.Bounds.X, Bounds.Y, _screen.Bounds.Left + tsTargetInfo.Bounds.Width, 36)
        If _isTargetInfoDropped Then
            Dim dropDownRect As Rectangle = _mdf.Bounds
            If Not textRect.Contains(cursorLoc) AndAlso Not dropDownRect.Contains(cursorLoc) Then
                MouseOverTimer.Stop()
                _mdf.RollUp()
                _isTargetInfoDropped = False
            End If
        ElseIf _isPCInfoDropped Then
            Dim dropDownRect As Rectangle = _pcf.Bounds
            If Not textRect.Contains(cursorLoc) AndAlso Not dropDownRect.Contains(cursorLoc) Then
                MouseOverTimer.Stop()
                _pcf.RollUp()
                _isPCInfoDropped = False
            End If
        End If
    End Sub

    ''' <summary>
    ''' This timer will watch for any new spawned pol processes
    ''' </summary>
    ''' <param name="sender"></param>
    ''' <param name="e"></param>
    ''' <remarks></remarks>
    Private Sub processWatchTimer_Tick(ByVal sender As System.Object, ByVal e As System.EventArgs) Handles processWatchTimer.Tick
        LoadAvailableProcesses()
    End Sub

    Private Sub NewsTimer_Tick(ByVal sender As System.Object, ByVal e As System.EventArgs) Handles NewsTimer.Tick
        LoadRSS()
    End Sub

#End Region

#Region " PRIVATE METHODS "
    Private Shared Function CheckForPOL() As Boolean
        Dim p As Process() = Process.GetProcessesByName("pol")
        If p.Length > 0 Then
            Return True
        Else
            Return False
        End If
    End Function

    Private Function InitializeProcess() As Boolean
        Dim p As Process() = Process.GetProcessesByName("pol")
        If p.Length > 0 Then
            If p.Length > 1 Then
                Dim spd As New SelectProcessDialog(p)
                If spd.ShowDialog = Windows.Forms.DialogResult.OK Then
                    _ffxi = New FFXI(spd.Process)
                Else
                    _ffxi = Nothing
                End If
            Else
                _ffxi = New FFXI(p(0))
            End If
        End If
        Return Not _ffxi Is Nothing
    End Function

    Private Function BuildMobInfo() As String
        Dim sb As New System.Text.StringBuilder
        _mem.Address = _ffxi.MemLocs("MAPBASE")
        Dim name As String = _tData.Name
        Dim zone As Integer = _mem.GetByte
        Dim mob = (From mobs In DataAccess.MobData.Mobs Where _
                   mobs.MobName = name And mobs.Zone = zone _
                   Select mobs).FirstOrDefault

        If Not mob Is Nothing Then
            sb.Append(mob.MobName)
            If mob.NM = 1 Then
                sb.Append(" [NM]")
            End If
            sb.Append(Environment.NewLine)
            If Not mob.IsFamilyNull Then
                sb.Append(String.Format("Family:{0}{0}{1}{2}", ControlChars.Tab, mob.Family & "", Environment.NewLine))
            Else
                sb.Append(String.Format("Family:{0}", Environment.NewLine))
            End If
            If Not mob.IsJobNull Then
                sb.Append(String.Format("Job:{0}{0}{1}{2}", ControlChars.Tab, mob.Job & "", Environment.NewLine))
            Else
                sb.Append(String.Format("Job:{0}", Environment.NewLine))
            End If
            sb.Append(String.Format("Behavior:{0}", ControlChars.Tab))
            sb.Append(GetBehavior(mob))
            sb.Append(Environment.NewLine)
            sb.Append(GetDetection(mob))
            sb.Append(Environment.NewLine)
            sb.Append(String.Format("Level Range:{0}{1}-{2}", ControlChars.Tab, mob.MinLevel, mob.MaxLevel))

            Dim mobId As Integer = mob.MobPK
            Dim items = (From i In DataAccess.MobData.Items Join _
                         im In DataAccess.MobData.ItemsToMobs On _
                         i.ItemID Equals im.ItemID Where _
                         im.MobPK = mobId Select New With {i.ItemName})

            If Not items Is Nothing Then
                Dim isFirst As Boolean = True
                sb.Append(Environment.NewLine)
                sb.Append(String.Format("Drops:{0}{0}", ControlChars.Tab))
                For Each item In items
                    If isFirst Then
                        sb.Append(item.ItemName & Environment.NewLine)
                        isFirst = False
                    Else
                        sb.Append(String.Format("{0}{0}{1}{2}", ControlChars.Tab, item.ItemName, Environment.NewLine))
                    End If
                Next
            End If
        End If
        Return sb.ToString
    End Function

    Private Shared Function GetBehavior(ByVal Mob As MobsRow) As String
        Dim output As String
        If Mob.Aggressive Then
            output = "Aggressive"
        Else
            output = String.Empty
        End If
        If Mob.Links Then
            If output = String.Empty Then
                output = "Links"
            Else
                output &= ", Links"
            End If
        End If
        Return output
    End Function

    Private Shared Function GetDetection(ByVal Mob As MobsRow) As String
        Dim output As String
        If Mob.DetectsSight Then
            output = "S"
        Else
            output = String.Empty
        End If
        If Mob.DetectsSound Then
            If output = String.Empty Then
                output = "H"
            Else
                output &= ", H"
            End If
        End If
        If Mob.DetectsMagic Then
            If output = String.Empty Then
                output = "M"
            Else
                output &= ", M"
            End If
        End If
        If Mob.DetectsLowHP Then
            If output = String.Empty Then
                output = "↓HP"
            Else
                output &= ", ↓HP"
            End If
        End If
        If Mob.DetectsHealing Then
            If output = String.Empty Then
                output = "Heal"
            Else
                output &= ", Heal"
            End If
        End If
        If Mob.TracksScent Then
            If output = String.Empty Then
                output = "Sc"
            Else
                output &= ", Sc"
            End If
        End If
        If Mob.TrueSight Then
            If output = String.Empty Then
                output = "T(S)"
            Else
                output &= ", T(S)"
            End If
        End If
        If Mob.TrueSound Then
            If output = String.Empty Then
                output = "T(H)"
            Else
                output &= ", T(H)"
            End If
        End If
        If output <> String.Empty Then
            output = String.Format("Detects:{0}{0}{1}", ControlChars.Tab, output)
        End If
        Return output
    End Function

    Friend Sub showBrowser(ByVal url As String)
        Dim targetHeight As Integer = _screen.Bounds.Height - 236
        BrowserForm.Browser.Navigate(url)
        BrowserForm.Location = New Point(Left + 106, Height)
        BrowserForm.Width = _screen.Bounds.Width - 106
        If targetHeight < 800 Then
            BrowserForm.Height = _screen.Bounds.Height - 236
        Else
            BrowserForm.Height = 800
        End If
        BrowserForm.Opacity = Opacity
        BrowserForm.Show()
    End Sub

    ''' <summary>
    ''' Method to load any available pol processes
    ''' </summary>
    ''' <remarks></remarks>
    Private Sub LoadAvailableProcesses()
        'Get the current list of availabel processes
        _pList = From p In Process.GetProcesses Where p.ProcessName = "pol"

        Dim processMenu As Object

        If NoBar Then
            processMenu = ntsSelectProcess
        Else
            processMenu = tsProcess
        End If

        'Check to see if any processes are found
        If _pList.Count > 0 Then
            'First loop through the drop down items and remove any that
            'are not in the process list
            Dim isFound As Boolean = False
            Dim displayName As String

            For i = processMenu.DropDownItems.Count - 1 To 0 Step -1
                isFound = False
                For Each p As Process In _pList
                    If processMenu.DropDownItems(i).Tag = p.Id Then
                        displayName = String.Format("{0} [{1}]", p.MainWindowTitle, p.Id)
                        processMenu.DropDownItems(i).Text = displayName
                        If p.Id = _currentProcessID Then
                            processMenu.Text = displayName
                        End If
                        isFound = True
                        Exit For
                    End If
                Next
                If Not isFound Then
                    processMenu.DropDownItems.RemoveAt(i)
                End If
            Next

            'Next loop though the processes and add any that 
            'are not already in the list
            Dim tsi As ToolStripItem

            For Each p As Process In _pList
                isFound = False
                For Each item As ToolStripItem In processMenu.DropDownItems
                    If item.Tag = p.Id Then
                        isFound = True
                    End If
                Next
                If Not isFound AndAlso p.MainWindowTitle.Trim <> String.Empty Then
                    tsi = processMenu.DropDownItems.Add(String.Format("{0} [{1}]", p.MainWindowTitle, p.Id))
                    tsi.Tag = p.Id
                    AddHandler tsi.Click, AddressOf processItem_click
                End If
            Next

            'If there is only one process we go ahead and select it.
            If processMenu.DropDownItems.Count = 1 AndAlso processMenu.DropDownItems(0).Tag <> _currentProcessID Then
                If Not processMenu.DropDownItems(0).Text.StartsWith("PlayOnline") AndAlso _
                Not processMenu.DropDownItems(0).Text.StartsWith("[") Then
                    processMenu.DropDownItems(0).PerformClick()
                End If
            End If
        Else
            _currentProcessID = 0
            If processMenu.DropdownItems.Count > 0 Then
                processMenu.DropDownItems.Clear()
            End If
            'We Didn't find any pol processes
            If _isProcessSelected Then _isProcessSelected = False
            If processMenu.text <> "FFXI Not Found" Then
                processMenu.Text = "FFXI Not Found"
            End If
            processMenu = Nothing

            _pList = Nothing

        End If
    End Sub

    Private Sub ApplySettings()
        If Not _orf Is Nothing AndAlso Not _orf.IsDisposed Then
            _orf.OverlayRadar.TextRendering = My.Settings.TextRendering
            _orf.OverlayRadar.SmoothingMode = My.Settings.SmoothingMode
            _orf.OverlayRadar.CompositingQuality = My.Settings.CompositingQuality
        End If

        If Not _mrf Is Nothing AndAlso Not _mrf.IsDisposed Then
            _mrf.MapRadar.TextRendering = My.Settings.TextRendering
            _mrf.MapRadar.SmoothingMode = My.Settings.SmoothingMode
            _mrf.MapRadar.CompositingQuality = My.Settings.CompositingQuality
        End If

        tsPosition.Visible = My.Settings.ShowPosition
        positionSpacer.Visible = My.Settings.ShowPosition

        tsZone.Visible = My.Settings.ShowZoneTimer
        zoneSpacer.Visible = My.Settings.ShowZoneTimer

        tsClock.Visible = My.Settings.ShowClock
        clockSpacer.Visible = My.Settings.ShowClock

        If My.Settings.AutomaticNewsUpdates Then
            If Not NewsTimer.Enabled Then
                NewsTimer.Interval = My.Settings.NewsCheckInterval * 60000
                NewsTimer.Start()
            End If
        Else
            If NewsTimer.Enabled Then
                NewsTimer.Stop()
            End If
        End If

        If My.Settings.Monitor = "Primary" Then

            _screen = Screen.PrimaryScreen
            If Not _abm Is Nothing Then
                _abm.Monitor = MonitorType.Primary
            End If
        Else
            If Not _abm Is Nothing Then
                If Screen.AllScreens.Count > 1 Then
                    If Screen.AllScreens(1) Is Screen.PrimaryScreen Then
                        _screen = Screen.AllScreens(0)
                    Else
                        _screen = Screen.AllScreens(1)
                    End If
                    _abm.Monitor = MonitorType.Secondary
                Else
                    _abm.Monitor = MonitorType.Primary
                End If
            End If
        End If
        LoadRSS()

        ApplyTheme()
    End Sub

    Private Sub ApplyTheme()
        BackgroundImage = ThemeHandler.BarImage
        Dim fColor As Color = ThemeHandler.BarForeColor
        MainMenu.ForeColor = fColor
        CloseMenu.ForeColor = fColor
        If Not _abm Is Nothing Then
            If tsAutoHideBar.Checked Then
                If ThemeHandler.ActiveTheme.DockPosition = DockMode.Top Then
                    Location = New Point(_screen.Bounds.Left, 0)
                Else
                    Location = New Point(_screen.Bounds.Left, _screen.Bounds.Bottom - 36)
                End If
            Else
                _abm.DockMode = ThemeHandler.ActiveTheme.DockPosition
            End If
        End If
        'Let's handle the open forms and apply the themes
        Dim fBgColor As Color = ThemeHandler.FormBackgroundColor
        For Each f As Form In My.Application.OpenForms
            If Not TypeOf f Is AppBarForm Then
                MainModule.ApplyTheme(f)
            End If
        Next
    End Sub

    Private Sub LoadRSS()
        Try
            Dim rss As New RSS
            rss.LoadFeed("http://apradar.com/external.php?type=RSS2&forumids=13")
            Dim rti As RSSToolStrip
            Dim latestDate As Date
            Dim count As Integer = 0
            'Clear out any rss entries
            For i = tsRSS.DropDownItems.Count - 1 To 1 Step -1
                tsRSS.DropDownItems.RemoveAt(i)
            Next

            'Add any new rss items
            For Each item As RSSItem In rss.FeedItems
                tsRSS.DropDownItems.Add(New ToolStripSeparator)
                If item.PubDate > latestDate Then
                    latestDate = item.PubDate
                End If
                rti = New RSSToolStrip()
                rti.FeedViewer.FeedItem = item
                AddHandler rti.FeedViewer.lnkTitle.LinkClicked, AddressOf RSS_LinkClicked

                'rti.Height = 135
                tsRSS.DropDownItems.Add(rti)

                count += 1
                If count = My.Settings.MaxNewsItems Then
                    Exit For
                End If
            Next


            If latestDate > My.Settings.LastRSSDate Then
                My.Settings.LastRSSDate = latestDate
                tsRSS.Image = My.Resources.rssNew
            End If
        Catch ex As Exception

        End Try
    End Sub

    Private Sub SaveWatchList()
        Serializer.SerializeToXML(Of List(Of Integer))(Application.StartupPath & "\Data\WatchList.dat", WatchList)
    End Sub

    Private Sub LoadWatchList()
        If IO.File.Exists(Application.StartupPath & "\Data\WatchList.dat") Then
            Try
                WatchList = Serializer.DeserializeFromXML(Of List(Of Integer))(Application.StartupPath & "\Data\WatchList.dat")
            Catch ex As Exception

            End Try
        End If
    End Sub

    Friend Sub AddTargetedMobToWatch()
        If Not SpawnList.Contains(_tData.ServerID) Then
            SpawnList.Add(_tData.ServerID)
        End If
    End Sub

    Private Sub ProcessTarget()
        If Not _tData Is Nothing Then

            'Handle the pc entries
            If _tData.MobType = MobTypes.PC Then
                'LightSteelBlue is the standatrd
                tsTargetInfo.ForeColor = ThemeHandler.PCColor
                tsTargetInfo.Text = String.Format("PC [0x{0}] {1} - HP: {2}% ", _tData.ID.ToString("X"), _
                                                     _tData.Name, _tData.HP)
                If _tData.ID <> _lastTargetId Then
                    _lastTargetId = _tData.ID
                End If

                If DataAccess.AddPC(_tData.Name, _tData.ServerID) Then
                    _pcf.PCID = _tData.ServerID
                Else
                    _pcf.PCID = -1
                End If
                _mdf.MobID = -1
                tsAddMob.Visible = False
            Else 'Handle any npc entries
                'Tomato is the standard
                tsTargetInfo.ForeColor = ThemeHandler.NPCColor
                tsTargetInfo.Text = String.Format("NPC [0x{0}] {1} HP: {2}% ", _tData.ID.ToString("X"), _
                                                     _tData.Name, _tData.HP)
                If _tData.ID <> _lastTargetId Then
                    '_targetInfoData = BuildMobInfo()
                    _mdf.Zone = _currentZone
                    Dim name As String = _tData.Name
                    _mob = (From mob In DataAccess.MobData.Mobs _
                            Where mob.MobName = name And mob.Zone = _mdf.Zone).FirstOrDefault
                    If _mob Is Nothing Then
                        tsAddMob.Visible = True
                        _mdf.MobID = -1
                    Else
                        tsAddMob.Visible = False
                        _mdf.MobID = _mob.MobPK
                    End If
                    _lastTargetId = _tData.ID
                    _pcf.PCID = -1

                End If


            End If
        Else
            _mdf.MobID = -1
            _pcf.PCID = -1
            tsAddMob.Visible = False
            tsTargetInfo.ForeColor = MainMenu.ForeColor
            tsTargetInfo.Text = "No Target"
            _targetInfoData = String.Empty

        End If
    End Sub
#End Region

#Region " EVENT HANDLERS "
    Private Sub _orf_Disposed(ByVal sender As Object, ByVal e As EventArgs) Handles _orf.Disposed
        tsOverlayRadar.Visible = False
    End Sub

    Private Sub _mrf_Disposed(ByVal sender As Object, ByVal e As EventArgs) Handles _mrf.Disposed
        tsMappedRadar.Visible = False
    End Sub

    ''' <summary>
    ''' This method handles the click event for the process items.
    ''' </summary>
    ''' <param name="sender"></param>
    ''' <param name="e"></param>
    ''' <remarks></remarks>
    Private Sub processItem_click(ByVal sender As Object, ByVal e As System.EventArgs)
        If Not _targetTimer Is Nothing Then
            _targetTimer.Stop()
        End If

        Dim tsi As ToolStripItem = DirectCast(sender, ToolStripItem)
        tsi.OwnerItem.Text = tsi.Text

        'Set the current process text
        _ffxi = Nothing

        'Reset the FFXI object
        _ffxi = New FFXI(Process.GetProcessById(tsi.Tag))

        Scanner.ScannerSingleton.FFXI = _ffxi

        _isProcessSelected = True
        _currentProcessID = tsi.Tag

        CurrentZoneTimer.Start()
    End Sub

    Private Sub CampedMobManager_DebugEvent(ByVal Location As String, ByVal EventType As String, ByVal Message As String)
        If Not _debugForm Is Nothing AndAlso Not _debugForm.IsDisposed Then
            _debugForm.AddDebugMessage(EventType, String.Format("{0} - {1}", Location, Message))
        End If
    End Sub

    Private Sub MobSpawned(ByVal Mob As MobData, ByVal OutOfRange As Boolean)
        If GlobalSettings.IsProEnabled Then
            If OutOfRange Then
                If My.Settings.SupressToast AndAlso My.Settings.PlayAlertOnSpawn Then
                    AudioManager.PlayAlert(My.Settings.AlertSound)
                Else
                    Dim message As String = String.Format("{0} spawned!{1}{2}{1}Out of range spawn detected!",
                                                          Mob.Name, ControlChars.NewLine, Now.ToLongTimeString)
                    Dim timeout As Integer = 0
                    If My.Settings.UseAlertTimeout Then
                        timeout = 15
                    End If
                    ShowToastForm(message, String.Format("{0} spawned!", Mob.Name), timeout, My.Settings.PlayAlertOnSpawn)
                End If
            Else
                'If we aren't showing the toast form, lets just play the alert
                If My.Settings.SupressToast AndAlso My.Settings.PlayAlertOnSpawn Then
                    AudioManager.PlayAlert(My.Settings.AlertSound)
                Else
                    Dim message As String = String.Format("{0} spawned!{3}{6}{3}X: {1}, Y: {2}{3}Distance: {4}{3}HP: {5}",
                                                            Mob.Name, Mob.X, Mob.Y, ControlChars.NewLine, Mob.Distance, Mob.HP, Now.ToLongTimeString)
                    Dim timeout As Integer = 0
                    If My.Settings.UseAlertTimeout Then
                        timeout = 15
                    End If
                    ShowToastForm(message, String.Format("{0} spawned!", Mob.Name), timeout, True)
                End If
            End If

            'Lets show the tracker form
            If My.Settings.ShowTracker Then
                Dim isTrackerUP As Boolean = False
                For Each t As SpawnTrackerForm In TrackForms
                    If t.MobServerID = Mob.ServerID Then
                        isTrackerUP = True
                        Exit For
                    End If
                Next
                If Not isTrackerUP Then
                    Dim stf As New SpawnTrackerForm(Mob.MobBase, _pData.MobBase, _ffxi)
                    Dim r As New RECT
                    'Modify the height to stagger the windows
                    Dim hMod As Integer = 0
                    If TrackForms.Count > 0 AndAlso TrackForms.Count Mod 2 <> 0 Then
                        hMod = 20
                    End If
                    If GetWindowRect(_ffxi.POL.MainWindowHandle, r) Then
                        stf.Location = New Point(r.Left + ((r.Right - r.Left) / 2) - (stf.Width / 2) + 60 * TrackForms.Count, r.Top + 75 + hMod)
                    Else
                        stf.Location = New Point(Screen.PrimaryScreen.WorkingArea.Width / 2 - stf.Width / 2 + 60 * TrackForms.Count, 75 + hMod)
                    End If
                    stf.Show()
                    stf.TransparencyKey = Color.Black
                    stf.BringToFront()
                    AddHandler stf.Disposed, AddressOf TrackForm_Disposed
                    TrackForms.Add(stf)
                End If
            End If

        End If
    End Sub

    Private Sub TrackForm_Disposed(ByVal sender As Object, ByVal e As EventArgs)
        TrackForms.Remove(sender)
    End Sub
#End Region

#Region " WATCHER EVENTS "
    Private Sub _mobWatcher_NewMobList(ByVal InMobs As MobList) Handles _mobWatcher.NewMobList
        _tData = InMobs.Item(Scanner.ScannerSingleton.TargetID)
        _pData = InMobs.Item(Scanner.ScannerSingleton.MyID)

        Me.Mobs = InMobs.ToList

        If My.Settings.ShowPosition Then
            tsPosition.Text = String.Format("X: {0} Y: {1} Z: {2} [{3}° {4}]", _pData.X.ToString("0.000"), _
                                               _pData.Y.ToString("0.000"), _pData.Z.ToString("0.000"), _
                                               Math.Round(RadiansToDegrees(_pData.Direction), 2), GetHeading(_pData.Direction))
        End If

        ProcessTarget()

        If CampingMode AndAlso GlobalSettings.IsProEnabled Then
            For Each mob As MobData In Me.Mobs
                If ValidateCampledMob(mob.Name) Then
                    _isMobCamped = SetCampedMobStatus(mob.ServerID, mob.ID, mob.Name, (mob.HP <= 0 AndAlso mob.WarpInfo = 0), DateTime.Now, New PointF(mob.X, mob.Y))
                End If
            Next
        End If
    End Sub

    Private Sub _mobWatcher_MobStatusChanged(ByVal mob As MobData, ByVal status As MobList.MobStatus) Handles _mobWatcher.MobStatusChanged
        If SpawnWatching AndAlso GlobalSettings.IsProEnabled Then
            If status = MobList.MobStatus.Alive Then
                'We have a mob that has just spawned so lets show the alert
                If SpawnList.Contains(mob.ServerID) OrElse WatchList.Contains(mob.ServerID) Then
                    'We either aren't filtering spawns or the mob was found in our spanw list
                    'so let's popup the toast form
                    MobSpawned(mob, False)
                End If
            ElseIf My.Settings.EnableOutOfRange AndAlso mob.Distance < 50.0 AndAlso status = MobList.MobStatus.OutOfRange Then
                'The mob has either moved out of range or has spawned out of range
                If SpawnList.Contains(mob.ServerID) OrElse WatchList.Contains(mob.ServerID) Then
                    MobSpawned(mob, True)
                End If
            End If
        End If
    End Sub

    Private Sub _mobWatcher_ZoneChanged(ByVal LastZone As Byte, ByVal NewZone As Byte) Handles _mobWatcher.ZoneChanged
        ZoneTimer.Reset()
        ZoneTimer.Start()
        _currentZone = NewZone
        _zoneName = Zones.GetZoneName(NewZone)
    End Sub
#End Region

#Region " CAMPING MODE METHODS "
    Private Shared Function ValidateCampledMob(ByVal Name As String) As Boolean
        Return Name.Trim <> String.Empty AndAlso Name <> "NPC" AndAlso Name <> "Moogle" AndAlso _
        Not Name.StartsWith("Door:") AndAlso Name <> "Home Point"
    End Function

    Private Function SetCampedMobStatus(ByVal MobServerID As Integer, ByVal Id As Integer, ByVal Name As String, ByVal IsDead As Boolean, ByVal ToD As DateTime?, ByVal Position As PointF) As Boolean
        Dim todString As String
        If Not IsDead Then
            todString = String.Empty
        Else
            todString = CDate(ToD).ToString("G")
        End If

        If CampedMobExists(MobServerID) Then
            Dim cMobs As CampedMob() = CampedMobManager.GetCampedMobs
            Dim cm = (From c In cMobs Where c.ServerID = MobServerID).FirstOrDefault
            If IsDead <> cm.IsDead OrElse cm.Position <> Position Then
                cm.IsDead = IsDead
                cm.DeathTime = todString
                cm.Position = Position
                CampedMobManager.ModifyCampedMobs(cm, CampedMobManager.EditType.Update)
            End If
        Else
            Dim cm As New CampedMob(Name, Id, MobServerID, _currentZone, IsDead, todString, Position)
            CampedMobManager.AddCampedMob(cm)
        End If
        Return IsDead
    End Function

    Private Shared Function CampedMobExists(ByVal MobServerID As Integer)
        Dim cMobs As CampedMob() = CampedMobManager.GetCampedMobs()
        Return (From c In cMobs Where c.ServerID = MobServerID).FirstOrDefault IsNot Nothing
    End Function
#End Region


End Class
